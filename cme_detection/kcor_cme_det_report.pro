; docformat = 'rst'

;+
; Send an email report about a completed CME.
;
; :Params:
;   time : in, required, type=
;-
pro kcor_cme_det_report, time
  compile_opt strictarr
  common kcor_cme_detection

  if (n_elements(speed_history) gt 0L) then begin
    time = tai2utc(tairef, /time, /truncate, /ccsds)

    ; Look for the file containing the email addresses. If not found, then
    ; simply return.
    addressfile = getenv('KCOR_MAILING_LIST')
    if (~file_exist(addressfile)) then begin
      mg_log, 'no address file specified in KCOR_MAILING_LIST, not sending email', $
              name='kcor-cme', /warn
      return
    endif

    ; create a temporary file for the message
    mailfile = mk_temp_file(dir=get_temp_dir(), 'cme_mail.txt', /random)

    ; Write out the message to the temporary file. Different messages are sent
    ; depending on whether the alert was automatic or generated by the operator.
    openw, out, mailfile, /get_lun

    printf, out, 'The Mauna Loa K-coronagraph has detected a possible CME ending at ' + $
            time + ' UT with the following parameters'
    printf, out
    format = '(F10.2)'
    printf, out, strjoin(strtrim(reform(speed_history), 2), ', '), $
            format='(%"Speed history: %s km/s")'

    free_lun, out

    ; form a subject line for the email
    subject = 'MLSO K-cor report for CME ending at ' + time + ' UT'

    ; step through the address file, and send a message to each listed recipient
    openr, in, addressfile, /get_lun
    address = ''
    while (~eof(in)) do begin
      readf, in, address
      address = strtrim(address, 2)
      if (address ne '') then begin
        cmd = string(subject, address, mailfile, $
                     format='(%"nohup mail -s \"%s\" %s < %s &")')
        spawn, cmd, result, error_result, exit_status=status
        if (status eq 0L) then begin
          mg_log, 'report sent to %s', address, name='kcor-cme', /info
        endif else begin
          mg_log, 'problem with mail command: %s', cmd, name='kcor-cme', /error
          mg_log, strjoin(error_result, ' '), name='kcor-cme', /error
        endelse
      endif
    endwhile
    free_lun, in

    ; delete the temporary file
    file_delete, mailfile


    delvarx, speed_history
  endif
end
