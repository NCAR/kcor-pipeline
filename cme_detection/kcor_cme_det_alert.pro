;+
; Project     :	MLSO - KCOR
;
; Name        :	KCOR_CME_DET_ALERT
;
; Purpose     :	Generates an alert when a CME is detected
;
; Category    :	KCOR, CME, Detection
;
; Explanation :	This routine handles the actions involved in alerting users
;               that a CME has been detected.  At the moment this involves
;               writing a message to a widget, but future actions could include
;               generating movies and sending emails.
;
; Syntax      :	KCOR_CME_DET_ALERT, ITIME, RSUN
;               KCOR_CME_DET_ALERT, ITIME, /OPERATOR
;
; Examples    :	See KCOR_CME_DET_MEASURE, KCOR_CME_DET_EVENT
;
; Inputs      :	ITIME   = Current time index into LEADINGEDGE array
;               RSUN    = Solar radii in arcminutes.  Ignored if /OPERATOR
;                         keyword is set.
;
; Opt. Inputs :	None
;
; Outputs     :	None
;
; Opt. Outputs:	None
;
; Keywords    :	OPERATOR = Flags that this is an operator alert, and not all
;                          the CME parameters are available.
;
; Calls       :	TAI2UTC, KCOR_CME_DET_MOVIE, KCOR_CME_DET_EMAIL
;
; Common      :	KCOR_CME_DETECTION defined in kcor_cme_detection.pro
;
; Restrictions:	None
;
; Side effects:	None
;
; Prev. Hist. :	None
;
; History     :	Version 1, 05-Jan-2017, William Thompson, GSFC
;               Version 2, 22-Mar-2017, WTT, call KCOR_CME_DET_MOVIE,
;                       KCOR_CME_DET_EMAIL.  Add OPERATOR keyword.
;
; Contact     :	WTHOMPSON
;-
pro kcor_cme_det_alert, itime, rsun, operator=operator
  compile_opt strictarr
  @kcor_cme_det_common

  current_cme_tai = tairef
  last_interim_report = tairef
  current_cme_start_time = tai2utc(tairef, /truncate, /ccsds) + 'Z'

  last_detected_image_tairef = date_diff[n_elements(detected) - 1L].tai_avg
  last_detected_image_time = tai2utc(last_detected_image_tairef, /time, /truncate, /ccsds)

  ; Format the message based on whether the alert was automatic, or generated by
  ; the operator.
  if (keyword_set(operator)) then begin
    tairef = date_diff[itime].tai_avg
    time = tai2utc(tairef, /time, /truncate, /ccsds)
    mg_log, 'Operator-generated alert at ' + time + ' UT', name='kcor/cme', /warn
  endif else begin
    time = tai2utc(tairef, /time, /truncate, /ccsds)
    edge = 60 * (lat[leadingedge[itime]] + 90) / rsun
    mg_log, 'CME detected at %s UT', time, name='kcor/cme', /warn
    mg_log, 'Rsun           : %s', ntrim(edge, '(F0.2)'), name='kcor/cme', /warn
    mg_log, 'position angle : %s deg', ntrim(angle, '(F0.1)'), name='kcor/cme', /warn
    mg_log, 'initial speed  : %s km/s', ntrim(speed, '(F0.1)'), name='kcor/cme', /warn
  endelse

  mg_log, 'CME detected when images up to %s UT have been processed', $
          last_detected_image_time, $
          name='kcor/cme', /warn

  ; update list of CMEs
  list_dir = run->config('cme/list_dir')
  if (n_elements(list_dir) gt 0L) then begin
    mg_log, 'Updating CME list... ', name='kcor/cme', /info
    kcor_cme_update_list, simple_date, time, angle, 'all', list_dir
  endif

  kcor_cme_det_movie
  kcor_cme_det_email, time, edge, last_detected_image_time, operator=operator

  alerts_basedir = run->config('cme/alerts_basedir')
  if (n_elements(alerts_basedir) gt 0L) then begin
    alerts_dir = filepath('', $
                          subdir=kcor_decompose_date(simple_date), $
                          root=alerts_basedir)
    if (~file_test(alerts_dir, /directory)) then file_mkdir, alerts_dir
  endif

  ftp_url = run->config('cme/ftp_alerts_url')
  if (n_elements(alerts_dir) gt 0L || n_elements(ftp_url) gt 0L) then begin
    issue_time = kcor_cme_current_time(run=run)
    start_time = tai2utc(utc2tai(date_diff[itime].date_obs), /truncate, /ccsds) + 'Z'
    time_for_height = tai2utc(tairef, /truncate, /ccsds) + 'Z'
    mode = run->config('cme/mode')
    alert_json = kcor_cme_alert_initial(issue_time, $
                                        last_data_time, $  ; TODO: or last_sci_data_time?
                                        start_time, $
                                        mode, $
                                        position_angle=angle, $
                                        speed=speed, $
                                        height=edge, $
                                        time_for_height=time_for_height)

    json_filename = kcor_cme_alert_filename(start_time, issue_time)
    kcor_cme_alert_text2file, alert_json, json_filename
    kcor_db_alert_initial_ingest, alert_json

    if (n_elements(ftp_url) gt 0L) then begin
      ftp_from_email = run->config('cme/from_email')
      if (n_elements(ftp_from_email) eq 0L) then ftp_from_email = ''

      kcor_cme_ftp_transfer, ftp_url, json_filename, ftp_from_email, $
                             status=ftp_status, $
                             error_msg=ftp_error_msg, $
                             cmd=ftp_cmd
      if (ftp_status ne 0L) then begin
        mg_log, 'FTP transferred with error %d', ftp_status, name='kcor/cme', /error
        mg_log, 'FTP command: %s', ftp_cmd, name='kcor/cme', /error
        for e = 0L, n_elements(ftp_error_msg) - 1L do begin
          mg_log, ftp_error_msg[e], name='kcor/cme', /error
        endfor
      endif
    endif

    if (n_elements(alerts_dir) gt 0L) then file_copy, json_filename, alerts_dir

    file_delete, json_filename, /allow_nonexistent
  endif

  ; If called with /OPERATOR, then delete the temporary definition of TAIREF to
  ; allow automatic CME detection to continue.
  if (keyword_set(operator)) then delvarx, tairef
end
